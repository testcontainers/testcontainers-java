buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.ch.myniva.gradle:s3-build-cache:0.10.0"
        classpath "com.gradle.enterprise:com.gradle.enterprise.gradle.plugin:3.10.3"
        classpath "com.gradle:common-custom-user-data-gradle-plugin:1.7.2"
    }
}

apply plugin: 'com.gradle.enterprise'
apply plugin: "com.gradle.common-custom-user-data-gradle-plugin"

rootProject.name = 'testcontainers-examples'

includeBuild '..'

// explicit include to allow Dependabot to autodiscover subprojects
include 'disque-job-queue'
include 'kafka-cluster'
include 'linked-container'
include 'mongodb-container'
include 'neo4j-container'
include 'redis-backed-cache'
include 'redis-backed-cache-testng'
include 'selenium-container'
include 'singleton-container'
include 'solr-container'
include 'spring-boot'
include 'cucumber'
include 'spring-boot-kotlin-redis'
include 'spock'

ext.isCI = System.getenv("CI") != null

buildCache {
    local {
        enabled = !isCI
    }
    remote(HttpBuildCache) {
        push = isCI && !System.getenv("READ_ONLY_REMOTE_GRADLE_CACHE")
        enabled = true
        url = 'https://ge.testcontainers.org/cache/'
        credentials {
            username = 'ci'
            password = System.getenv("GRADLE_ENTERPRISE_CACHE_PASSWORD")
        }
    }
}

gradleEnterprise {
    buildScan {
        server = "https://ge.testcontainers.org/"
        publishAlways()
        publishIfAuthenticated()
        uploadInBackground = !isCI
        captureTaskInputFiles = true
    }

}
