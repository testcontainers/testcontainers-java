buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.ch.myniva.gradle:s3-build-cache:0.10.0"
        classpath "com.gradle.enterprise:com.gradle.enterprise.gradle.plugin:3.2.1"
    }
}

apply plugin: 'ch.myniva.s3-build-cache'
apply plugin: 'com.gradle.enterprise'

rootProject.name = 'testcontainers-java'

include "bom"

include "testcontainers"
project(':testcontainers').projectDir = "$rootDir/core" as File

file('modules').eachDir { dir ->
    include dir.name
    project(":${dir.name}").projectDir = dir
}

file('providers').eachDir {dir ->
    include "providers-${dir.name}"
    project(":providers-${dir.name}").projectDir = dir
}

include ':docs:examples:junit4:generic'
include ':docs:examples:junit4:redis'
include ':docs:examples:junit5:redis'
include ':docs:examples:spock:redis'

include 'test-support'

ext.isMasterBuild = false ||
    (System.getenv("GITHUB_REF") == "refs/heads/master") ||
    (System.getenv("BUILD_SOURCEBRANCHNAME") == "master")

buildCache {
    local {
        enabled = !isMasterBuild
    }

    remote(ch.myniva.gradle.caching.s3.AwsS3BuildCache) {
        endpoint = 'fra1.digitaloceanspaces.com'
        region = 'fra1'
        bucket = 'testcontainers'
        path = 'cache'
        reducedRedundancy = false
        push = isMasterBuild && !System.getenv("READ_ONLY_REMOTE_GRADLE_CACHE")
        headers = [
            'x-amz-acl': 'public-read'
        ]
    }
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
