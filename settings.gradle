buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.enterprise:com.gradle.enterprise.gradle.plugin:3.7.2"
        classpath "com.gradle:common-custom-user-data-gradle-plugin:1.6.1"
    }
}

apply plugin: 'com.gradle.enterprise'
apply plugin: "com.gradle.common-custom-user-data-gradle-plugin"

rootProject.name = 'testcontainers-java'

include "bom"

include "testcontainers"
project(':testcontainers').projectDir = "$rootDir/core" as File

file('modules').eachDir { dir ->
    include dir.name
    project(":${dir.name}").projectDir = dir
}

include ':docs:examples:junit4:generic'
include ':docs:examples:junit4:redis'
include ':docs:examples:junit5:redis'
include ':docs:examples:spock:redis'

include 'test-support'

ext.isMasterBuild = false ||
    (System.getenv("GITHUB_REF") == "refs/heads/master") ||
    (System.getenv("BUILD_SOURCEBRANCHNAME") == "master")

ext.isCI = System.getenv("CI") != null

buildCache {
    local {
        enabled = !isCI
    }
    remote(HttpBuildCache) { 
        push = isCI
        enabled = true
        url = 'https://ge.testcontainers.org/cache/' 
        credentials {
            username = 'ci'
            password = System.getenv("GRADLE_ENTERPRISE_CACHE_PASSWORD")
        }
    }
}

gradleEnterprise {
    buildScan {
        server = "https://ge.testcontainers.org/"
        publishAlways()
        publishIfAuthenticated()
        uploadInBackground = !isCI
        captureTaskInputFiles = true
    }

}
