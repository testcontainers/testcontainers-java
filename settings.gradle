buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.enterprise:com.gradle.enterprise.gradle.plugin:3.19.2"
        classpath "com.gradle:common-custom-user-data-gradle-plugin:2.0.2"
        classpath "org.gradle.toolchains:foojay-resolver:0.8.0"
    }
}

apply plugin: 'com.gradle.develocity'
apply plugin: "com.gradle.common-custom-user-data-gradle-plugin"
apply plugin: "org.gradle.toolchains.foojay-resolver-convention"

rootProject.name = 'testcontainers-java'

include "bom"

include "testcontainers"
project(':testcontainers').projectDir = "$rootDir/core" as File

file('modules').eachDir { dir ->
    include dir.name
    project(":${dir.name}").projectDir = dir
}

include ':docs:examples:junit4:generic'
include ':docs:examples:junit4:redis'
include ':docs:examples:junit5:redis'
include ':docs:examples:spock:redis'

include 'test-support'

ext.isCI = System.getenv("CI") != null

buildCache {
    local {
        enabled = !isCI
    }
    remote(develocity.buildCache) {
        push = isCI && !System.getenv("READ_ONLY_REMOTE_GRADLE_CACHE") && System.getenv("DEVELOCITY_ACCESS_KEY")
        enabled = true
    }
}

develocity {
    buildScan {
        server = "https://ge.testcontainers.org/"
        publishing.onlyIf {
            it.authenticated
        }
        uploadInBackground = !isCI
        capture.fileFingerprints = true
    }

}
