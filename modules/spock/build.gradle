plugins {
    id 'groovy'
}

description = "Testcontainers :: Spock-Extension"

dependencies {
    api project(':testcontainers')
    implementation 'org.spockframework:spock-core:2.3-groovy-4.0'

    testImplementation project(':selenium')
    testImplementation project(':mysql')
    testImplementation project(':postgresql')

    testImplementation 'com.zaxxer:HikariCP:6.3.0'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.14'

    testRuntimeOnly 'org.postgresql:postgresql:42.7.7'
    testRuntimeOnly 'mysql:mysql-connector-java:8.0.33'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-testkit:1.13.3'

    testCompileOnly 'org.jetbrains:annotations:26.0.2'
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

sourceJar {
    /* allJava is default (see gradle/publishing.gradle:sourceJar)
       allSource contains both .java and .groovy files */
    from sourceSets.main.allSource
}

javadocJar {
    dependsOn groovydoc
    archiveClassifier = 'javadoc'
    from groovydoc.destinationDir
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
