plugins {
    id 'groovy'
}

description = "Testcontainers :: Spock-Extension"

dependencies {
    api project(':testcontainers')
    implementation 'org.spockframework:spock-core:2.3-groovy-4.0'

    testImplementation project(':testcontainers-selenium')
    testImplementation project(':testcontainers-mysql')
    testImplementation project(':testcontainers-postgresql')

    testImplementation 'com.zaxxer:HikariCP:7.0.2'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.14'

    testRuntimeOnly 'org.postgresql:postgresql:42.7.8'
    testRuntimeOnly 'com.mysql:mysql-connector-j:9.5.0'
    testRuntimeOnly platform('org.junit:junit-bom:5.14.1')
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'org.junit.platform:junit-platform-testkit'

    testCompileOnly 'org.jetbrains:annotations:26.0.2-1'
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

sourceJar {
    /* allJava is default (see gradle/publishing.gradle:sourceJar)
       allSource contains both .java and .groovy files */
    from sourceSets.main.allSource
}

javadocJar {
    dependsOn groovydoc
    archiveClassifier = 'javadoc'
    from groovydoc.destinationDir
}
