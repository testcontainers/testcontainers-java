plugins {
    id 'com.gradle.build-scan' version '2.3'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'nebula.provided-base' version '3.0.3'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id "com.jfrog.bintray" version "1.8.4" apply false
}

subprojects {
    apply plugin: 'nebula.provided-base'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply from: "$rootDir/gradle/publishing.gradle"
    apply from: "$rootDir/gradle/bintray.gradle"

    group = "org.testcontainers"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'

    lombok {
        version = '1.18.6'
    }

    task delombok(type: io.franzbecker.gradle.lombok.task.DelombokTask) {
        def outputDir = file("$buildDir/delombok")
        outputs.dir(outputDir)
        for (srcDir in project.sourceSets.main.java.srcDirs) {
            inputs.dir(srcDir)
            args(srcDir, "-d", outputDir)
        }
    }

    project.tasks.sourceJar.from(delombok)

    task release(dependsOn: bintrayUpload)

    test {
        defaultCharacterEncoding = "UTF-8"
        testLogging {
            displayGranularity 1
            showStackTraces = true
            exceptionFormat = 'full'
            events "STARTED", "PASSED", "FAILED", "SKIPPED"
        }
    }

    repositories {
        jcenter()
    }

    shadowJar {
        configurations = []
        classifier = null

        doFirst {
            // See https://github.com/johnrengelman/shadow/blob/5.0.0/src/main/groovy/com/github/jengelman/gradle/plugins/shadow/tasks/ConfigureShadowRelocation.groovy
            Set<String> packages = []
            // Always read from core's configurations
            for (configuration in tasks.getByPath(":testcontainers:shadowJar").configurations) {
                for (jar in configuration.files) {
                    def jf = new java.util.jar.JarFile(jar)
                    for (entry in jf.entries()) {
                        def name = entry.name
                        if (name.endsWith(".class")) {
                            packages.add(name.substring(0, name.lastIndexOf('/')))
                        }
                    }
                    jf.close()
                }
            }
            for (pkg in packages) {
                pkg = pkg.replaceAll('/', '.')

                if (pkg.startsWith("com.github.dockerjava.")) {
                    // Keep docker-java's package inside the final jar
                    continue;
                }

                tasks.shadowJar.relocate(pkg, "org.testcontainers.shaded.${pkg}")
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) { publication ->
                artifacts.removeAll { it.classifier == null }
                artifact project.tasks.shadowJar
            }
        }
    }

    dependencies {
        testCompile 'ch.qos.logback:logback-classic:1.2.3'
    }
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}
