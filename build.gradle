plugins {
    id 'nebula.provided-base' version '3.0.3'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

subprojects {
    apply plugin: 'nebula.provided-base'
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = "org.testcontainers"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'

    if (project.name != "core") {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    artifactId = project.name

                    pom.withXml {
                        asNode().appendNode('name', project.description)
                        asNode().dependencies.'*'
                            .findAll() { project.configurations.provided.allDependencies.find { dep -> dep.name == it.artifactId.text() } }
                            .each() { it.scope*.value = 'provided' }
                    }
                }
            }
        }
    }

    test.testLogging {
        displayGranularity 1
        showStackTraces = true
        exceptionFormat = 'full'
        events "STARTED", "PASSED", "FAILED", "SKIPPED"
    }

    test.doFirst {
        mkdir project.file('target')
    }

    repositories {
        jcenter()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.16.20'
        testCompileOnly 'org.projectlombok:lombok:1.16.20'

        testCompile 'ch.qos.logback:logback-classic:1.2.3'
    }
}
