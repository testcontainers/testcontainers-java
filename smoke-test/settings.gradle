buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.ch.myniva.gradle:s3-build-cache:0.10.0"
        classpath "com.gradle.enterprise:com.gradle.enterprise.gradle.plugin:3.14.1"
        classpath "com.gradle:common-custom-user-data-gradle-plugin:1.11.1"
    }
}

apply plugin: 'com.gradle.enterprise'
apply plugin: "com.gradle.common-custom-user-data-gradle-plugin"

rootProject.name = 'testcontainers-smoke-tests'

includeBuild '..'

// explicit include to allow Dependabot to autodiscover subprojects
include 'turbo-mode'
include 'config'

ext.isCI = System.getenv("CI") != null

buildCache {
    local {
        enabled = !isCI
    }
    remote(HttpBuildCache) {
        push = isCI && !System.getenv("READ_ONLY_REMOTE_GRADLE_CACHE")
        enabled = true
        url = 'https://ge.testcontainers.org/cache/'
        credentials {
            username = 'ci'
            password = System.getenv("GRADLE_ENTERPRISE_CACHE_PASSWORD")
        }
    }
}

gradleEnterprise {
    buildScan {
        server = "https://ge.testcontainers.org/"
        publishAlways()
        publishIfAuthenticated()
        uploadInBackground = !isCI
        captureTaskInputFiles = true
    }

}
