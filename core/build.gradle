apply plugin: 'com.github.johnrengelman.shadow'

description = "Testcontainers Core"

sourceSets {
    jarFileTest
}

idea.module.testSourceDirs += sourceSets.jarFileTest.allSource.srcDirs

configurations {
    shaded
    [runtime, compileOnly, testCompile]*.extendsFrom shaded
}

shadowJar {
    configurations = [project.configurations.shaded]

    mergeServiceFiles()

    exclude 'org/newsclub/**'

    [
        'META-INF/NOTICE',
        'META-INF/NOTICE.txt',
        'META-INF/LICENSE',
        'META-INF/LICENSE.txt',
        'META-INF/DEPENDENCIES',
        'META-INF/maven/',
        'META-INF/proguard/',
        'META-INF/services/README.md',
        'META-INF/services/com.fasterxml.jackson.core.*',
        'META-INF/services/com.github.dockerjava.api.command.*',
        'META-INF/services/javax.ws.rs.ext.*',
        'META-INF/services/java.security.Provider',
        'mozilla/public-suffix-list.txt',
    ].each { exclude(it) }

    project.afterEvaluate {
        dependencies {
            for (id in project.configurations.compile.resolvedConfiguration.resolvedArtifacts*.moduleVersion*.id) {
                exclude(dependency("${id.group}:${id.name}"))
            }
        }
    }
}

task jarFileTest(type: Test) {
    testClassesDirs = sourceSets.jarFileTest.output.classesDirs
    classpath = sourceSets.jarFileTest.runtimeClasspath

    systemProperty("jarFile", shadowJar.outputs.files.singleFile)

    dependsOn(shadowJar)
}
project.tasks.check.dependsOn(jarFileTest)

dependencies {
    compile 'junit:junit:4.12'
    compile 'org.slf4j:slf4j-api:1.7.26'
    compile 'org.jetbrains:annotations:17.0.0'
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'org.apache.commons:commons-compress:1.18'
    // Added for JDK9 compatibility since it's missing this package
    compile 'javax.xml.bind:jaxb-api:2.3.1'
    compile ('org.rnorth.duct-tape:duct-tape:1.0.8') {
        exclude(group: 'org.jetbrains', module: 'annotations')
    }

    compile 'org.rnorth.visible-assertions:visible-assertions:2.1.2'

    compile ('org.rnorth:tcp-unix-socket-proxy:1.0.2') {
        exclude(group: "log4j", module: "log4j")
    }

    compile "net.java.dev.jna:jna-platform:5.3.1"

    shaded ('org.scala-sbt.ipcsocket:ipcsocket:1.0.0') {
        exclude(group: "net.java.dev.jna")
    }

    shaded ('com.github.docker-java:docker-java:3.1.0-rc-4') {
        exclude(group: 'org.glassfish.jersey.core')
        exclude(group: 'org.glassfish.jersey.connectors')
        exclude(group: 'org.glassfish.jersey.inject')
        exclude(group: 'log4j')
        exclude(group: 'com.google.code.findbug')
        exclude(group: 'com.kohlschutter.junixsocket')
        exclude(group: 'io.netty')
        exclude(group: 'org.apache.httpcomponents')
        exclude(group: 'org.slf4j')
        exclude(group: 'org.apache.commons', module: 'commons-compress')
    }

    shaded 'com.squareup.okhttp3:okhttp:3.14.2'

    shaded 'org.glassfish.main.external:trilead-ssh2-repackaged:4.1.2'

    shaded 'org.zeroturnaround:zt-exec:1.11', {
        exclude(group: 'org.slf4j')
    }

    testCompile 'org.apache.httpcomponents:httpclient:4.5.9'
    testCompile 'redis.clients:jedis:3.0.1'
    testCompile 'com.rabbitmq:amqp-client:5.7.1'
    testCompile 'org.mongodb:mongo-java-driver:3.10.2'
    testCompile ('org.mockito:mockito-core:2.28.2') {
        exclude(module: 'hamcrest-core')
    }
    // Synthetic JAR used for MountableFileTest and DirectoryTarResourceTest
    testCompile files('testlib/repo/fakejar/fakejar/0/fakejar-0.jar')


    jarFileTestCompileOnly "org.projectlombok:lombok:${lombok.version}"
    jarFileTestCompile 'junit:junit:4.12'
    jarFileTestCompile 'org.assertj:assertj-core:3.12.2'
    jarFileTestCompile 'org.ow2.asm:asm-debug-all:5.2'
}
